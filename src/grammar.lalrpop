use crate::ast::untyped::{Expr, Stmt, FuncCall, FuncDeclare, Literal};
use crate::ast::Span;
use crate::typing::{ProtoType};

grammar;

match {
    // delimiters
    "{", "}", "(", ")", "[", "]", ";", ":", ".", ",", "|",

    // declaration
    "let",

    // operators
    "=", "==", ">", "<", "<=", ">=", "+", "-", "*", "/",

    // syntax constructs
    "=>", "if", "elif", "else", "loop", "break",

    // literals
    "true", "false",
    r"[0-9]+",         // int
    r"[0-9]+\.[0-9]+", // float
    r#""[^"]*""#,      // string

    // symbols
    r"[a-zA-Z_][a-zA-Z_0-9]*",  

}

pub Program: Expr = {
    <start: @L> <exprs:StmtList> <end: @R> => Expr::Block(exprs, Span { start, end })
};

StmtList: Vec<Stmt> = {
    <first:Expr> ";" <rest:StmtList> => {
        let mut stmts = vec![Stmt { expr: first }];
        stmts.extend(rest);
        stmts
    },
    <expr:Expr> ";" => vec![Stmt { expr }],
};

Expr: Expr = {
    Assignment,
    Equality,
    IfElse,
    Loop,
    Break,
};

TypeExpr: ProtoType = {
    Identifier => ProtoType::Atomic(<>),
    <base:Identifier> "<" <args:Comma<TypeExpr>> ">" => ProtoType::Applied(base, args),
};

IfElse: Expr = {
    "if" <cond:Expr> "{" <start: @L> <then_block:StmtList> <end: @R> "}" <tail:ElifOrElse> => {
        Expr::IfElse(Box::new(cond), Box::new(Expr::Block(then_block, Span { start, end })), Box::new(tail))
    },

    "if" <cond:Expr> "{" <start: @L> <then_block:StmtList> <end: @R>"}" => {
        Expr::If(Box::new(cond), Box::new(Expr::Block(then_block, Span { start, end })))
    },
};

ElifOrElse: Expr = {
    "elif" <cond:Expr> "{" <start: @L> <then_block:StmtList> <end: @R>"}" <tail:ElifOrElse> => {
        Expr::IfElse(Box::new(cond), Box::new(Expr::Block(then_block, Span { start, end })), Box::new(tail))
    },

    "elif" <cond:Expr> "{" <start:@L> <then_block:StmtList> <end:@R> "}" => {
        Expr::If(Box::new(cond), Box::new(Expr::Block(then_block, Span { start, end })))
    },

    "else" "{" <start: @L> <else_block:StmtList> <end: @R>"}" => {
        Expr::Block(else_block, Span { start, end })
    },
};

Loop: Expr = {
    "loop" "{" <start: @L> <body:StmtList> <end: @R>"}" => Expr::Loop(Box::new(Expr::Block(body, Span { start, end }))),
};

Break: Expr = {
    "break" => Expr::Break,
};

Assignment: Expr = {
    "let" <i:Identifier> "=" <v:Expr> => Expr::Assignment(i, Box::new(v)),
};

Equality: Expr = {
    <l:Equality> "==" <r:Relational> => Expr::Eq(Box::new(l), Box::new(r)),
    Relational,
};

Relational: Expr = {
    <l:Relational> ">" <r:Addition> => Expr::Gt(Box::new(l), Box::new(r)),
    <l:Relational> "<" <r:Addition> => Expr::Lt(Box::new(l), Box::new(r)),
    <l:Relational> ">=" <r:Addition> => Expr::Gte(Box::new(l), Box::new(r)),
    <l:Relational> "<=" <r:Addition> => Expr::Lte(Box::new(l), Box::new(r)),
    Addition,
};

Addition: Expr = {
    <l:Addition> "+" <r:Multiplication> => Expr::Add(Box::new(l), Box::new(r)),
    <l:Addition> "-" <r:Multiplication> => Expr::Sub(Box::new(l), Box::new(r)),
    Multiplication,
};

Multiplication: Expr = {
    <l:Multiplication> "*" <r:Unary> => Expr::Mult(Box::new(l), Box::new(r)),
    <l:Multiplication> "/" <r:Unary> => Expr::Div(Box::new(l), Box::new(r)),
    Unary,
};

Unary: Expr = {
    "-" <expr:Unary> => Expr::Negate(Box::new(expr)),
    PostfixExpression,
};

PostfixExpression: Expr = {
    <start: @L> <primary:Primary> "(" <args:ExprList> ")" <end: @R> => Expr::FuncCall(FuncCall { func: Box::new(primary), args }, Span { start, end }),
    <start: @L> <primary:Primary> "(" ")" <end: @R> => Expr::FuncCall(FuncCall { func: Box::new(primary), args: vec![] }, Span { start, end }),
    Primary,
};

ExprList: Vec<Expr> = Comma<Expr>;

// Stubbed out for now
Primary: Expr = {
    Literal => Expr::Literal(<>),
    Identifier => Expr::Identifier(<>),
    FuncDeclare,
    ListLiteral,
};

Literal: Literal = {
    Int => Literal::Int(<>),
    Float => Literal::Float(<>),
    Str => Literal::String(<>),
    Bool => Literal::Bool(<>),
}

Identifier: String = {
    r"[a-zA-Z_][a-zA-Z_0-9]*" => <>.to_string(),
};

FuncDeclare: Expr = {
    "(" <params:ParameterList> ")" ":" <return_type:TypeExpr> "=>" "{" <start: @L> <stmts:StmtList> <end: @R> "}" => Expr::FuncDeclare(FuncDeclare{ params, return_type: Box::new(return_type), block: Box::new(Expr::Block(stmts, Span { start, end })), is_closure: false }),
    "(" ")" ":" <return_type:TypeExpr> "=>" "{" <start: @L> <stmts:StmtList> <end: @R> "}" => Expr::FuncDeclare(FuncDeclare{ params: vec![], return_type: Box::new(return_type), block: Box::new(Expr::Block(stmts, Span { start, end })), is_closure: false }),
    "|" <params:ParameterList> "|" ":" <return_type:TypeExpr> "=>" "{" <start: @L> <stmts:StmtList> <end: @R> "}" => Expr::FuncDeclare(FuncDeclare{ params, return_type: Box::new(return_type), block: Box::new(Expr::Block(stmts, Span { start, end })), is_closure: true }),
    "|" "|" ":" <return_type:TypeExpr> "=>" "{" <start: @L> <stmts:StmtList> <end: @R> "}" => Expr::FuncDeclare(FuncDeclare{ params: vec![], return_type: Box::new(return_type), block: Box::new(Expr::Block(stmts, Span { start, end })), is_closure: true }),

};

ListLiteral: Expr = {
    "[" <items:ExprList> "]" => Expr::List(items),
    "[" "]" => Expr::List(vec![]),
};

ParameterList: Vec<(String, ProtoType)> = Comma<Parameter>;

// Stubbed to support type annotations.
Parameter: (String, ProtoType) = {
    <ident:Identifier> ":" <ty:TypeExpr> => (ident, ty),
};

Bool: bool = {
    "true" => true,
    "false" => false,
};

Int: i32 = {
    r"[0-9]+" => <>.parse().unwrap(),
};

Float: f64 = {
    r"[0-9]+\.[0-9]+" => <>.parse().unwrap(),
};

Str: String = {
    r#""[^"]*""# => <>[1..<>.len()-1].to_string(),  // Remove the first and last characters (the quotes)
};

// Utility

Comma<T>: Vec<T> = {
    <item:T> "," <rest:Comma<T>> => {
        let mut list = vec![item];
        list.extend(rest);
        list
    },
    <item:T> => vec![item],
};